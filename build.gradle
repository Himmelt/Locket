apply plugin: 'java'

version = '1.2.2'
group = 'org.soraworld'

sourceCompatibility = targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    maven { url = 'https://maven.aliyun.com/repository/public/' }
    jcenter()
    maven { url = 'https://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

dependencies {
    compile 'org.soraworld:hocon:1.2.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: "maven-publish"

    group rootProject.group
    version rootProject.version

    sourceCompatibility = targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        maven { url = 'https://maven.aliyun.com/repository/public/' }
        jcenter()
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url = 'https://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    }

    configurations {
        shadow
        compile.extendsFrom(shadow)
        shadeOnly
        compileOnly.extendsFrom(shadeOnly)
    }

    dependencies {
        shadeOnly rootProject
    }

    jar {
        configurations.shadeOnly.copyRecursive().setTransitive(false).each {
            artifact -> from(zipTree(artifact))
        }
        includeEmptyDirs = false
        exclude 'net/minecraft/server', 'org/bukkit/craftbukkit'
    }

    publishing {
        repositories {
            maven {
                name = "locket"
                url = uri("https://maven.pkg.github.com/himmelt/locket")
                credentials {
                    username = 'Himmelt'
                    password = System.getenv("PACKAGES_TOKEN")
                }
            }
        }
    }
}
